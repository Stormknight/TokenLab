[h: title = "Spontaneous Spell Resources"]
[h: activeToken = json.get( macro.args, "ID" ) ]
[h: characterName = getProperty("Character", activeToken ) ]
[h: Resources = json.get( macro.args, "Resources" )]


[h: imageFormatString = 	"<img src='%s' border=0 height=45 width=45 alt='%s'>"]
[h: IncrementImage = 	    strformat(imageFormatString, tblImage( "TokenLabImages",  2 ), 	"Add one to resource used")]
[h: DecrementImage =        strformat(imageFormatString, tblImage("TokenLabImages", 3 ), 	"Subtract one to resource used")]


[frame(title, "input=1; height=100; width=100"):{
	<html>
	<head>
	    <title>[r: title]</title>
	    <link rel="stylesheet" type="text/css" href="css@Lib:TokenLab">
	</head>
	<body>
	<h2><br>[r:characterName] Spontaneous Spells</h2>
	<br>

                     [h:resetArgs = json.set( "{}", "ID", activeToken, "Resources", resources  )]
                     [r:macroLink("Reset all", "UDF_ResetSpontaneousSpellResources@Lib:TokenLab", "self", resetArgs, activeToken)]

                      [h:index = 0]
	<table>
                     [FOREACH ( spellLevelResources, Resources, "" ), CODE: {
                                           [h:spellClassName = json.get( spellLevelResources, "spellClassName" ) ]
		[h:spellLevel = json.get( spellLevelResources, "spellLevel" ) ]
		[h:spells = json.get( spellLevelResources, "spells" ) ]
		[h:unlimited = json.get( spellLevelResources, "unlimited" ) ]
		[h:used = json.get( spellLevelResources, "used" ) ]
		[h:maxCasts = json.get( spellLevelResources, "maxCasts" ) ]
                                          [h: upargs   = json.set("{}",  "ID", activeToken, "direction", "up" , "index", index, "Resources", resources  )]
                                          [h: downargs   = json.set("{}",  "ID", activeToken, "direction", "down" , "index", index, "Resources", resources  )]

                                          [h: UpButton = macroLink(IncrementImage, 	"UDF_UpdateSpontaneousSpellResource@Lib:TokenLab", "none", upargs, activeToken )]
                                          [h: DownButton = macroLink(DecrementImage, 	"UDF_UpdateSpontaneousSpellResource@Lib:TokenLab", "none", downargs, activeToken )]

                                         <tr>
                                            <td><b><font size = "4" >[r:spellClassName] (level [r:spellLevel]) </font> </td>
                                            <td>[r:DownButton]</td>
                                            <td><b><font size = "6" >[r:used]</font></b> <font size="3">/ [r:maxCasts]</font></td>
                                            <td>[r:UpButton]</td>

                                        </tr>

                                        [h:index = index+1]
                      }]
                     </table>
	</body>
	</html>
}]




